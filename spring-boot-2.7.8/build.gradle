plugins {
	id "base"
	id "org.jetbrains.kotlin.jvm" apply false // https://youtrack.jetbrains.com/issue/KT-30276
	id "io.spring.nohttp" version "0.0.10"
}

description = "Spring Boot Build"

defaultTasks 'build'

nohttp {
	allowlistFile = project.file("src/nohttp/allowlist.lines")
	source.exclude "**/bin/**"
	source.exclude "**/build/**"
	source.exclude "**/out/**"
	source.exclude "**/target/**"
	source.exclude "spring-boot-project/spring-boot-tools/spring-boot-buildpack-platform/src/test/resources/org/springframework/boot/buildpack/platform/docker/export.tar"
}

check {
	dependsOn checkstyleNohttp
}

allprojects {
	group "org.springframework.boot"

	repositories {
		mavenLocal()
		maven { url = uri("https://maven.aliyun.com/repository/central") } // central
		maven { url = uri("https://maven.aliyun.com/repository/public") } // jcenter & public
		maven { url = uri("https://maven.aliyun.com/repository/google") } // google
		maven { url = uri("https://maven.aliyun.com/repository/spring") } // spring
		maven { url = uri("https://maven.aliyun.com/repository/spring-plugin") } // spring plugin
		maven { url = uri("https://maven.aliyun.com/repository/grails-core") } // spring plugin
		mavenCentral()
		if (version.contains('-')) {
			maven { url "https://repo.spring.io/milestone" }
		}
		if (version.endsWith('-SNAPSHOT')) {
			maven { url "https://repo.spring.io/snapshot" }
		}
	}

	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 0, "minutes"
	}
}

//tasks.named("checkstyleNohttp").configure {
//	maxHeapSize = "1g"
//}
